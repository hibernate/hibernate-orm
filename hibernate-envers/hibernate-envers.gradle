apply plugin: org.hibernate.build.gradle.testing.matrix.MatrixTestingPlugin

dependencies {
    compile( project( ':hibernate-core' ) )
    compile( project( ':hibernate-entitymanager' ) )
    provided( [group: 'org.hibernate', name: 'hibernate-tools', version: '3.2.0.ga'] )
    provided( libraries.ant )
    testCompile( project(':hibernate-testing') )
    testRuntime( libraries.javassist )
}

sourceSets {
    main {
        ext.generatedJpaMetamodelSrcDir = file( "${buildDir}/generated-src/jpamodelgen/${name}" )
        java {
            srcDir generatedJpaMetamodelSrcDir
        }
    }
}

// Generate JPA2 static metamodel for default revision entities
task generateJpaMetamodelClasses(type: Compile) {
    classpath = compileJava.classpath + configurations.hibernateJpaModelGenTool
    source = sourceSets.main.originalJavaSrcDirs
    destinationDir = file( "${buildDir}/tmp/apt" )
    options.define(
            compilerArgs: [
                    "-proc:only",
                    "-processor", "org.hibernate.jpamodelgen.JPAMetaModelEntityProcessor",
                    "-s", "$sourceSets.main.generatedJpaMetamodelSrcDir.absolutePath"
            ]
    );
    outputs.dir sourceSets.main.generatedJpaMetamodelSrcDir;
    doFirst {
        sourceSets.main.generatedJpaMetamodelSrcDir.mkdirs()
    }
}
compileJava.dependsOn generateJpaMetamodelClasses

jar {
    manifest {
        instruction 'Bundle-Description', 'Hibernate ORM Envers'
        instruction 'Bundle-SymbolicName', 'org.hibernate.envers'
        
        instruction 'Import-Package',
            // optionals
            'javax.naming;resolution:=optional',
            'org.apache.tools.ant;resolution:=optional',
			'*'
    }
}
