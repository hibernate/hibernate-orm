/*
 * Hibernate, Relational Persistence for Idiomatic Java
 *
 * License: GNU Lesser General Public License (LGPL), version 2.1 or later.
 * See the lgpl.txt file in the root directory or <http://www.gnu.org/licenses/lgpl-2.1.html>.
 */
apply plugin: 'hibernate-matrix-testing'

dependencies {
    compile( project( ':hibernate-core' ) )

    provided( [group: 'org.hibernate', name: 'hibernate-tools', version: '3.2.0.ga'] )
    provided( libraries.ant )
    provided( project( ':hibernate-jpamodelgen' ) )

    testCompile( project( ':hibernate-testing' ) )
    testCompile( project( path: ':hibernate-core', configuration: 'tests' ) )
    testRuntime( libraries.javassist )
    testRuntime( libraries.byteBuddy )

    // ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    // Java 9 ftw!
    if ( JavaVersion.current().isJava9Compatible() ) {
        // The JDK used to run Gradle is Java 9+, and we assume that that is the same
        //		JDK for executing tasks
        provided( 'com.sun.xml.bind:jaxb-impl:2.2.11' )
        provided( 'org.glassfish.jaxb:jaxb-xjc:2.2.11' )
        provided( 'org.jvnet.jaxb2_commons:jaxb2-basics:0.11.0' )
        provided( 'org.jvnet.jaxb2_commons:jaxb2-basics-ant:0.11.0' )
        provided( 'javax:javaee-api:7.0' )

        testCompile( 'com.sun.xml.bind:jaxb-impl:2.2.11' )
        testCompile( 'org.glassfish.jaxb:jaxb-xjc:2.2.11' )
        testCompile( 'org.jvnet.jaxb2_commons:jaxb2-basics:0.11.0' )
        testCompile( 'org.jvnet.jaxb2_commons:jaxb2-basics-ant:0.11.0' )
        testCompile( 'javax:javaee-api:7.0' )

        testRuntime( 'com.sun.xml.bind:jaxb-impl:2.2.11' )
        testRuntime( 'org.glassfish.jaxb:jaxb-xjc:2.2.11' )
        testRuntime( 'org.jvnet.jaxb2_commons:jaxb2-basics:0.11.0' )
        testRuntime( 'org.jvnet.jaxb2_commons:jaxb2-basics-ant:0.11.0' )
        testRuntime( 'javax:javaee-api:7.0' )
    }
    // ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
}

mavenPom {
    name = 'ENtity VERSioning support'
    description = 'ENtity VERSioning support'
}

def osgiDescription() {
	return mavenPom.description
}

sourceSets {
    test {
        ext {
            enversDemoJavaDir = file( "src/demo/java" )
            enversDemoResourcesDir = file( "src/demo/resources" )
        }
        java {
            srcDir enversDemoJavaDir
        }
        resources {
            srcDir enversDemoResourcesDir
        }
    }
}

jar {
    manifest {
        instructionFirst 'Import-Package',
            // TODO: Remove after Javassist is blown away in ORM 6
            "javassist;version=\"${javassistVersion}\"",
            "javassist.util.proxy;version=\"${javassistVersion}\"",
            // TODO: Shouldn't have to explicitly list the JPA packages, but
            // the plugin generates them with [1.0,2) versions.
            'javax.persistence;version="2.1.0"',
            'javax.persistence.criteria;version="2.1.0"',
            'javax.persistence.metamodel;version="2.1.0"',
            'javax.persistence.spi;version="2.1.0"',
            // optionals
            'javax.naming;resolution:=optional',
            'org.apache.tools.ant;resolution:=optional'
    }
}

tasks."matrix_mariadb" {
    beforeTest { descriptor ->
        println "Starting test: " + descriptor
    }
}

if ( JavaVersion.current().isJava9Compatible() ) {
    logger.warn( '[WARN] Skipping Javassist-related tests for hibernate-envers due to Javassist JDK 9 incompatibility' )

    // we need to exclude tests using Javassist enhancement, which does not properly support
    // Java 9 yet - https://issues.jboss.org/browse/JASSIST-261
    test {
        // rather than wild-cards, keep an explicit list
        exclude 'org/hibernate/envers/internal/tools/MapProxyTest.class'
        exclude 'org/hibernate/envers/test/integration/components/dynamic/AuditedDynamicComponentTest.class'
        exclude 'org/hibernate/envers/test/integration/components/dynamic/AuditedDynamicComponentsAdvancedCasesTest.class'
    }
}