/*
 * SPDX-License-Identifier: Apache-2.0
 * Copyright Red Hat Inc. and Hibernate Authors
 */

plugins {
	id "local.java-module"
}

description = 'Integration tests for running Hibernate ORM in the Java module path'

// See https://docs.gradle.org/6.7.1/userguide/java_testing.html#blackbox_integration_testing
// See https://docs.gradle.org/6.7.1/samples/sample_java_modules_multi_project_with_integration_tests.html
java.modularity.inferModulePath = true


dependencies {
	api project( ':hibernate-core' )
	api project( ':hibernate-envers' )
	implementation project( ':hibernate-scan-jandex' )

    testImplementation testLibs.junitJupiterApi

	//Provide the jakarta.cdi module, as it's required by module jakarta.transaction
	//but not provided as transitive dependency of Narayana.
	testRuntimeOnly( jakartaLibs.cdi )
	testRuntimeOnly( jakartaLibs.interceptors )
}

test {
	useJUnitPlatform()
}

tasks.withType(JavaCompile).configureEach {
    // NOTE: we override the compiler args here since we don't want to include
    //  the `--limit-modules` defined in the `local.java-module`.
    //  This module is specific to module path testing and we require much much more than what's
    //  defined in the `local.java-module` (e.g. we need JPA or ORM modules as well as things like logging etc.)
    //
    //  We also are dropping the JBoss logging arg from the list since the tests do not have loggers that require
    //  impl generation.
    options.compilerArgs = [ ]
}
