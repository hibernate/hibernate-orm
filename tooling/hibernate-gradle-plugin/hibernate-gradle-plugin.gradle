import org.apache.tools.ant.filters.ReplaceTokens

/*
 * Hibernate, Relational Persistence for Idiomatic Java
 *
 * License: GNU Lesser General Public License (LGPL), version 2.1 or later.
 * See the lgpl.txt file in the root directory or <http://www.gnu.org/licenses/lgpl-2.1.html>.
 */

plugins {
	id 'java-gradle-plugin'
	id 'com.gradle.plugin-publish' version '0.20.0'

	id 'checkstyle'

	// for local publishing
	id 'maven-publish'
}

apply from: rootProject.file( 'gradle/module.gradle' )
apply from: rootProject.file( 'gradle/javadoc.gradle' )
apply from: rootProject.file( 'gradle/releasable.gradle' )

description = "Gradle plugin for integrating Hibernate aspects into your build"

def pluginId = 'org.hibernate.orm'

dependencies {
	implementation project(':hibernate-core')
	implementation libs.byteBuddy
	implementation jakartaLibs.jaxbApi

	implementation gradleApi()
	implementation localGroovy()

	// for Gradle
	implementation jakartaLibs.inject
	implementation localGroovy()

	testImplementation gradleTestKit()
	testImplementation testLibs.assertjCore
	testImplementation testLibs.junit5Api

	testRuntimeOnly testLibs.junit5Engine
}

gradlePlugin {
	plugins {
		ormPlugin {
			id = pluginId
			implementationClass = 'org.hibernate.orm.tooling.gradle.HibernateOrmPlugin'
		}
	}
}

pluginBundle {
	website = 'https://github.com/hibernate/hibernate-orm/tree/main/tooling/hibernate-gradle-plugin'
	vcsUrl = 'https://github.com/hibernate/hibernate-orm/tree/main/tooling/hibernate-gradle-plugin'
	tags = ['hibernate','orm','bytecode','enhancement','bytebuddy']

	// sigh
	mavenCoordinates {
		groupId = project.group.toString()
		artifactId = project.name
		version = project.version.toString()
	}

	plugins {
		ormPlugin {
			id = pluginId
			displayName = 'Gradle plugin for Hibernate ORM'
			description = 'Applies Hibernate aspects into the build'
		}
	}
}

test {
	useJUnitPlatform()
}

// Publish to the Gradle Plugin Portal
tasks.release.dependsOn tasks.publishPlugins

// local publishing (SNAPSHOT testing)
tasks.publish.dependsOn tasks.publishPlugins

// Make sure that the publishReleaseArtifacts task of the release module runs the release task of this sub module
tasks.getByPath( ':release:publishReleaseArtifacts' ).dependsOn tasks.release

// local publishing (SNAPSHOT testing)
publishing {
	repositories {
		maven {
			name = 'localPluginRepository'
			url = "${buildDir}/local-plugin-repository"
		}
	}
}

processResources {
	inputs.property( "orm-version", getVersion() )
	description = description + " (injected with Hibernate version)"
	filter( ReplaceTokens, tokens: [ 'hibernateVersion': getVersion() ] )
}

tasks.withType( JavaCompile ) {
	options.encoding = 'UTF-8'
}

if ( !jdkVersions.explicit ) {
	tasks.withType( GroovyCompile ) {
		sourceCompatibility = JavaVersion.toVersion( jdkVersions.baseline )
		targetCompatibility = JavaVersion.toVersion( jdkVersions.baseline )
	}
}
else {
	logger.warn( "[WARN] Toolchains are not yet supported for Groovy compilation." +
						 " Using the JDK that runs Gradle for Groovy compilation." )
}

tasks.named( "javadoc", Javadoc ) {
	configure( options ) {
		windowTitle = "Hibernate Javadocs ($project.name)"
		docTitle = "Hibernate Javadocs ($project.name : $project.version)"
	}
}

checkstyle {
	sourceSets = [ project.sourceSets.main ]
	configFile = rootProject.file( 'shared/config/checkstyle/checkstyle.xml' )
	showViolations = false
}

tasks.publish.enabled !project.ormVersion.isSnapshot
tasks.publishPlugins.enabled !project.ormVersion.isSnapshot

gradle.taskGraph.whenReady { tg ->
	// verify credentials for publishing the plugin up front to avoid any work (only if we are publishing)
	if ( tg.hasTask( ":publishPlugins" ) && project.tasks.publishPlugins.enabled  ) {
		// we are publishing the plugin - make sure there is a credentials pair
		//
		// first, check the `GRADLE_PUBLISH_KEY` / `GRADLE_PUBLISH_SECRET` combo (env vars)
		// and then the `gradle.publish.key` / `gradle.publish.secret` combo (project prop)
		//		- see https://docs.gradle.org/current/userguide/publishing_gradle_plugins.html#account_setup
		if ( System.getenv().get("GRADLE_PUBLISH_KEY") != null ) {
			if ( System.getenv().get("GRADLE_PUBLISH_SECRET") != null ) {
				throw new RuntimeException( "`GRADLE_PUBLISH_KEY` specified, but not `GRADLE_PUBLISH_SECRET` for publishing Gradle plugin" )
			}
		}
		else if ( project.findProperty( 'gradle.publish.key' ) != null ) {
			if ( project.findProperty( 'gradle.publish.secret' ) != null ) {
				throw new RuntimeException( "`gradle.publish.key` specified, but not `gradle.publish.secret` for publishing Gradle plugin" )
			}
		}
		else {
			throw new RuntimeException( "No credentials specified for publishing Gradle plugin" )
		}
	}

	// local publishing (SNAPSHOT testing), cont.
	//		- https://github.com/gradle-nexus/publish-plugin/issues/143
	//		- https://github.com/gradle-nexus/publish-plugin/pull/144
	tasks.withType(PublishToMavenRepository) { PublishToMavenRepository t ->
		if ( t.repository == null ) {
			logger.info( "Task `{}` had null repository", t.path )
		}
		else if ( t.repository.name == "sonatype" ) {
			logger.debug( "Disabling task `{}` because it publishes to Sonatype", t.path )
			t.enabled = false
		}
	}
}