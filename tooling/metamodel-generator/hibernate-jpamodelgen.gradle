/*
 * Hibernate, Relational Persistence for Idiomatic Java
 *
 * License: GNU Lesser General Public License (LGPL), version 2.1 or later.
 * See the lgpl.txt file in the root directory or <http://www.gnu.org/licenses/lgpl-2.1.html>.
 */
plugins {
    id 'org.hibernate.build.xjc-jakarta'
}

description = 'Hibernate compile-time tooling'

apply from: rootProject.file( 'gradle/published-java-module.gradle' )
apply plugin: 'version-injection'

ext {
	xjcTargetDir = file( "${buildDir}/generated/sources/xjc/main" )
	xsdDir = file( "${projectDir}/src/main/xsd" )
}

configurations {
	quarkusOrmPanache
	quarkusHrPanache
}

dependencies {
    // api - ewww... but Maven needs them this way
    api project( ':hibernate-core' )
    api jakartaLibs.jaxbApi
    api jakartaLibs.jaxb
    api jakartaLibs.validation
    api jakartaLibs.annotation
    api libs.antlrRuntime
    api libs.byteBuddy

    xjc jakartaLibs.xjc
    xjc jakartaLibs.jaxb
    xjc rootProject.fileTree(dir: 'patched-libs/jaxb2-basics', include: '*.jar')

	quarkusOrmPanache "io.quarkus:quarkus-hibernate-orm-panache:3.6.2"
	quarkusHrPanache "io.quarkus:quarkus-hibernate-reactive-panache:3.6.2"
}

def quarkusOrmPanacheTestTask = tasks.register( 'quarkusOrmPanacheTest', Test ) {
	description = 'Runs the Quarkus ORM Panache tests.'
	group = 'verification'

	testClassesDirs = sourceSets.test.output.classesDirs
	classpath = sourceSets.test.runtimeClasspath + configurations.quarkusOrmPanache
	shouldRunAfter test
	dependsOn test
}

def quarkusHrPanacheTestTask = tasks.register( 'quarkusHrPanacheTest', Test ) {
	description = 'Runs the Quarkus HR Panache tests.'
	group = 'verification'

	testClassesDirs = sourceSets.test.output.classesDirs
	classpath = sourceSets.test.runtimeClasspath + configurations.quarkusHrPanache
	shouldRunAfter test
	dependsOn test
}

check.dependsOn quarkusHrPanacheTestTask
check.dependsOn quarkusOrmPanacheTestTask

sourceSets.main {
    java.srcDir xjcTargetDir
	resources.srcDir xsdDir
}

compileTestJava {
    options.compilerArgs += [
            "-proc:none",
            "-AsuppressJakartaDataMetamodel=true"
    ]
}

sourceSets {
    test {
        java {
            exclude '**/data/*.java'
        }
    }
}

// Tests with records
if ( jdkVersions.test.release.asInt() >= 17 && jdkVersions.explicit ) {
    // We need to configure the source and target version to 17
    //compileTestJava17Java {
    compileTestJava {
        javaCompiler = javaToolchains.compilerFor {
            languageVersion = jdkVersions.test.compile
        }
        sourceCompatibility = 17
        targetCompatibility = 17
    }

    test {
        javaLauncher = javaToolchains.launcherFor {
            languageVersion = jdkVersions.test.launcher
        }
    }
}
else {
    sourceSets {
        test {
            java {
                exclude '**/records/*.java'
            }
        }
    }
}


task jaxb {
    // configure Gradle up-to-date checking
    inputs.dir( xsdDir ).withPropertyName("xsdDir" ).withPathSensitivity( PathSensitivity.RELATIVE )
    outputs.dir( xjcTargetDir )
    outputs.cacheIf { true }

    // perform actions
    doLast {
        xjcTargetDir.mkdirs()

        ant.taskdef(name: 'xjc', classname: 'org.jvnet.jaxb2_commons.xjc.XJC2Task', classpath: configurations.xjc.asPath)

        ant.xjc(
                destdir: ( xjcTargetDir as File ).absolutePath,
                package: 'org.hibernate.jpamodelgen.xml.jaxb',
                extension: 'true'
        ) {
            project.ant.arg line: '-no-header'
            project.ant.arg line: '-npa'
            schema( dir: xsdDir.path, includes: "*.xsd" )
        }
    }
}
tasks.sourcesJar.dependsOn jaxb
tasks.sourcesJar.dependsOn ':hibernate-core:generateHqlParser'
tasks.sourcesJar.dependsOn ':hibernate-core:generateSqlScriptParser'
tasks.compileJava.dependsOn jaxb

checkstyleMain.exclude '**/jaxb/**'

